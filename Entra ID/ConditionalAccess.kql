// Conditional Access - Audit on policy
AuditLogs 
| where Category == "Policy"
| project  ActivityDateTime, ActivityDisplayName , TargetResources[0].displayName, InitiatedBy.user.userPrincipalName

// Conditional Access - All successful authentications
SigninLogs
| where ConditionalAccessStatus == "success"
| project AppDisplayName, Identity, ConditionalAccessStatus  
 
// Conditional Access - Users allowed and users blocked
SigninLogs
| where TimeGenerated > ago (30d)
| project TimeGenerated, ConditionalAccessPolicies
| mv-expand ConditionalAccessPolicies
| extend CAResult = tostring(ConditionalAccessPolicies.result)
| extend CAPolicyName = tostring(ConditionalAccessPolicies.displayName)
| summarize CAResults=make_set(CAResult) by CAPolicyName
| where CAResults !has "success" and CAResults !has "failure"
 
// Conditional Access - Failures
SigninLogs
| where TimeGenerated > ago (30d)
| project TimeGenerated, ConditionalAccessPolicies, ResultType, ResultDescription
| mv-expand ConditionalAccessPolicies
| extend CAResult = tostring(ConditionalAccessPolicies.result)
| extend CAPolicyName = tostring(ConditionalAccessPolicies.displayName)
| where CAResult == "failure"
| summarize CAFailureCount=count()by CAPolicyName, ResultType, ResultDescription
| sort by CAFailureCount desc 
 
// Conditional Access - Failures (Graphical views)
let start = now(-90d);
let end = now();
let timeframe= 12h;
SigninLogs
| project TimeGenerated, ResultType, ConditionalAccessPolicies
| where ResultType == 53003
| extend FailedPolicy = tostring(ConditionalAccessPolicies[0].displayName)
| make-series FailureCount = count() default=0 on TimeGenerated in range(start,end, timeframe) by FailedPolicy
| render timechart 
 
// Conditional Access failures, return the reason for the failure
SigninLogs
| where TimeGenerated > ago (30d)
| project TimeGenerated, ConditionalAccessPolicies, ResultType, ResultDescription
| mv-expand ConditionalAccessPolicies
| extend CAResult = tostring(ConditionalAccessPolicies.result)
| extend CAPolicyName = tostring(ConditionalAccessPolicies.displayName)
| where CAResult == "failure"
| summarize CAFailureCount=count()by CAPolicyName, ResultType, ResultDescription
| sort by CAFailureCount desc 
 
// Create a pivot table showing all conditional access policy outcomes over the last 30 days
SigninLogs
| where TimeGenerated > ago(30d)
| extend CA = parse_json(ConditionalAccessPolicies)
| mv-expand bagexpansion=array CA
| evaluate bag_unpack(CA)
| extend
    ['CA Outcome']=tostring(column_ifexists('result', "")),
    ['CA Policy Name'] = column_ifexists('displayName', "")
| evaluate pivot(['CA Outcome'], count(), ['CA Policy Name'])
 
// Entra ID Conditional Access policies that have 0 hits for 'success' or 'failure' over the last month
SigninLogs
| where TimeGenerated > ago (30d)
| project TimeGenerated, ConditionalAccessPolicies
| mv-expand ConditionalAccessPolicies
| extend CAResult = tostring(ConditionalAccessPolicies.result)
| extend ['Conditional Access Policy Name'] = tostring(ConditionalAccessPolicies.displayName)
| summarize ['Conditional Access Result']=make_set(CAResult) by ['Conditional Access Policy Name']
| where ['Conditional Access Result'] !has "success"
    and ['Conditional Access Result'] !has "failure"
    and ['Conditional Access Result'] !has "unknownFutureValue"
| sort by ['Conditional Access Policy Name'] asc 

// Sign-ins targeted by Conditional Access, originating from an unknown network that don't require MFA
SigninLogs
| where AuthenticationRequirement != "multiFactorAuthentication"
| where HomeTenantId == ResourceTenantId
| where tostring(NetworkLocationDetails) == "[]"
| where ResultType == 0
| where ConditionalAccessStatus == "success"
| where todynamic(AuthenticationDetails)[0].authenticationMethod != "Windows Hello for Business"
| where AppDisplayName != "Microsoft Intune Company Portal"
| where AppDisplayName != "Microsoft Intune Web Company Portal"
| where AppDisplayName != "Microsoft Office Web Apps Service"
| where AppDisplayName != "Microsoft Account Controls V2"
| where ResourceDisplayName != "Microsoft Intune Enrollment"
| project UserPrincipalName, UserId, AppDisplayName, ResourceDisplayName, NetworkLocationDetails, AuthenticationRequirement
| summarize count() by UserPrincipalName, AppDisplayName, ResourceDisplayName, AuthenticationRequirement
| sort by UserPrincipalName asc, count_ desc
