// Source: https://github.com/microsoft/Microsoft-365-Defender-Hunting-Queries/tree/master/Protection%20events

// Detect Smart Screen App Blocks
let minTimeRange = ago(7d);
DeviceEvents
    | where ActionType == "SmartScreenAppWarning" and Timestamp > minTimeRange
            // Filter out SmartScreen test files downloaded from https://demo.smartscreen.msft.net/
            and not (FileName startswith "knownmalicious" and FileName endswith ".exe")
    | extend ParsedFields=parse_json(AdditionalFields)
    | project Timestamp, DeviceName, BlockedFileName=FileName, SHA1, Experience=tostring(ParsedFields.Experience), ActivityId=tostring(ParsedFields.ActivityId), InitiatingProcessFileName;
    
// Detect Smart Screen URL Warnings
 let minTimeRange = ago(7d);
 DeviceEvents
    | where ActionType == "SmartScreenUrlWarning" and Timestamp > minTimeRange
            // Filter out SmartScreen test URLs under https://demo.smartscreen.msft.net/
            and RemoteUrl !startswith "https://demo.smartscreen.msft.net/" 
    | extend ParsedFields=parse_json(AdditionalFields)
    | project Timestamp, DeviceName, BlockedUrl=RemoteUrl, Recommendation=tostring(ParsedFields.Recommendation), Experience=tostring(ParsedFields.Experience), ActivityId=tostring(ParsedFields.ActivityId);
